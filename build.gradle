plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2' //Comentar esta linea si no hace falta

}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.0'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'org.example.savemate'
    mainClass = 'org.example.savemate.Main'
}

javafx {
    version = '17.0.6'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.swing']
}

dependencies {
    implementation('org.controlsfx:controlsfx:11.1.2')
    implementation('com.dlsc.formsfx:formsfx-core:11.6.0') {
        exclude(group: 'org.openjfx')
    }
    implementation('net.synedra:validatorfx:0.4.0') {
        exclude(group: 'org.openjfx')
    }
    implementation('org.kordamp.ikonli:ikonli-javafx:12.3.1')
    implementation('org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0')
    implementation('eu.hansolo:tilesfx:11.48') {
        exclude(group: 'org.openjfx')
    }

    //JFoenix libreria complementaria para JavaFX
    implementation ('com.jfoenix:jfoenix:9.0.10')

    //Encriptador de contrase√±as
    implementation('org.mindrot:jbcrypt:0.4')

    //PostgresSQL
    implementation ('org.postgresql:postgresql:42.7.3')

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/SaveMate-${version}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages',  '--add-modules', 'jdk.crypto.ec']
    launcher {
        name = 'SaveMate'
    }

    jpackage {
        installerType = 'exe'
        installerName = 'SaveMateInstaller'
        appVersion = '1.0'
        icon = 'logo_multires.ico'
        vendor = 'Nestor'
        description = 'SaveMate - Gestor de gastos e ingresos personales.'
        installerOptions = [
                '--win-menu',
                '--win-shortcut',
                '--win-dir-chooser',
                '--win-menu-group', 'SaveMate'
        ]
    }
}

jlinkZip {
    group = 'distribution'
}

shadowJar {
    archiveClassifier.set('') // Esto hace que el jar generado se llame SaveMate.jar (sin -all)
}